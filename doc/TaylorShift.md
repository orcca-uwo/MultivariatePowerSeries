##ENCODING ISO-8859-1
##PROCEDURE(help,nospec,label="MultivariatePowerSeries/TaylorShift",versionnew="{XENOMORPH}",versionupdated="{ZONKEY}") MultivariatePowerSeries[TaylorShift]
##TITLE(halfline="perform a Taylor shift of power series or a univariate polynomial over power series")
##    MultivariatePowerSeries[TaylorShift]
##ALIAS TaylorShift, MultivariatePowerSeries:-TaylorShift, MultivariatePowerSeries
##AUTHOR Ali Asadi masadi4@uwo.ca, Alex Brandt abrandt5@uwo.ca, Marc Moreno Maza moreno@csd.uwo.ca
##
##CALLINGSEQUENCE
##- TaylorShift('p', 'e')
##- TaylorShift('u', 'c')
##
##PARAMETERS
##-(versionnew="{YETI}") 'p' : power series generated by this package
##-(versionnew="{YETI}") 'e' : one or more equations of the form ~x = c~, indicating that ~x~ should be shifted by ~c~;
##        'e' can be a single equation, or a list or set of equations.
##- 'c' : numeric value or algebraic number
##- 'u' : univariate polynomial over power series generated by this package
##DESCRIPTION
##- The command ~TaylorShift(p, e)~ returns the power series obtained by substituting ~x + c~ for
##  'x' in 'p' for each equation ~x = c~ in 'e'. This can only be computed from the "analytic
##  expression" of 'p'. If the analytic expression for 'p' is not known, an error is thrown.
##- The command ~TaylorShift(u, c)~ returns the univariate polynomial over power series obtained by substituting
##   ~v + c~ for 'v' in 'u', where 'v' is the main variable of 'u'.
##- A typical usage is when 'c' is a root of the polynomial returned by ~EvaluateAtOrigin(u)~. This happens, for example,
##  in "HenselFactorize".
##INCLUDE compatibility_warning.mi
##
##INCLUDE assignment_warning.mi
##
##EXAMPLES
##> with(MultivariatePowerSeries):
##- We define a power series for _sin(x) * exp(y)_.
##> sinx := PowerSeries(d -> ifelse(d :: even, 0, (-1)^((d - 1)/2) * x^d / d!), analytic = sin(x));
##<(verification="type") object
##> expy := PowerSeries(d -> y^d / d!, analytic = exp(y));
##<(verification="type") object
##> ps := sinx * expy;
##- We shift 'x' by 1 and 'y' by 2, performing all steps three times: all at once, 'x' first, or 'y'
##  first. (In practice, one would typically do both shifts at once: it is computationally more
##  efficient.)
##> ps_both := TaylorShift(ps, {x = 1, y = 2});
##> ps_x := TaylorShift(ps, x = 1);
##> ps_x_y := TaylorShift(ps_x, y = 2);
##> ps_y := TaylorShift(ps, y = 2);
##> ps_y_x := TaylorShift(ps_y, x = 1);
##- Let's take a look at the first few homogeneous components of 'ps_both'.
##> Truncate(ps_both, 4);
##< sin(1)*exp(2)+y*cos(1)*exp(2)*x-1/2*y*sin(1)*exp(2)*x^2-1/6*y*cos(1)*exp(2)*x^3+1/2*y^2*cos(1)*exp(2)*x-1/4*y^2*sin(1)
##  *exp(2)*x^2+1/6*y^3*cos(1)*exp(2)*x+cos(1)*exp(2)*x-1/2*sin(1)*exp(2)*x^2-1/6*cos(1)*exp(2)*x^3+1/24*sin(1)*exp(2)*x^4
##  +y*sin(1)*exp(2)+1/6*y^3*sin(1)*exp(2)+1/2*y^2*sin(1)*exp(2)+1/24*y^4*sin(1)*exp(2)
##- Now we verify that the three results, 'ps_both', 'ps_x_y' and 'ps_y_x', are equal (up to homogeneous degree 20).
##> ApproximatelyEqual(ps_both, ps_x_y, 20);
##< true
##> ApproximatelyEqual(ps_both, ps_y_x, 20);
##< true
##- We define a univariate polynomial over power series.
##> f := UnivariatePolynomialOverPowerSeries([PowerSeries(1), SumOfAllMonomials([x, y]), GeometricSeries(y)], z);
##<(verification="type") object
##- We apply a Taylor shift by 1, and then by -1 on the result.
##> f1 := TaylorShift(f, 1);
##<(verification="type") object
##> f0 := TaylorShift(f1, -1);
##<(verification="type") object
##- We verify that the result is equal to the original polynomial (up to homogeneous degree 20).
##> ApproximatelyEqual(f,f0,20);
##< true
##- Finally, we create a univariate polynomial over power series from a list of Puiseux series.
##> h := UnivariatePolynomialOverPowerSeries([PuiseuxSeries(1), PuiseuxSeries(0), PuiseuxSeries(x, [x=x^(1/3)]), PuiseuxSeries(y, [y=y^(1/2)]), PuiseuxSeries((x+y)/(1+x+y), [x=x*y^(1/2),y=x*y^(-1)])], z);
##<(verification="type") object
##- Note that the calling sequence ~TaylorShift(h, -1)~ will throw 
## 	an error since this function does not support univariate polynomials
##	over Puiseux series.
##> TaylorShift(h, -1);
##<(verification="testerror") "invalid"
##
##SEEALSO
##- "EvaluateAtOrigin"
##- "MainVariable"
##- "ApproximatelyEqual"
##- "UnivariatePolynomialOverPowerSeries"
##- "PuiseuxSeries"
##
##XREFMAP
##- analytic expression : Help:MultivariatePowerSeries:-GetAnalyticExpression
##- EvaluateAtOrigin : Help:MultivariatePowerSeries[EvaluateAtOrigin]
##- MainVariable : Help:MultivariatePowerSeries[MainVariable]
##- ApproximatelyEqual : Help:MultivariatePowerSeries[ApproximatelyEqual]
##- UnivariatePolynomialOverPowerSeries : Help:MultivariatePowerSeries[UnivariatePolynomialOverPowerSeries]
##- PuiseuxSeries : MultivariatePowerSeries[PuiseuxSeries]
##- HenselFactorize : Help:MultivariatePowerSeries[HenselFactorize]
