##ENCODING ISO-8859-1
##PROCEDURE(help,nospec,label="MultivariatePowerSeries/Add",versionnew="{XENOMORPH}") MultivariatePowerSeries[Add]
##TITLE(halfline="Add multivariate power series or univariate polynomials over power series")
##    MultivariatePowerSeries[Add]
##ALIAS Add, MultivariatePowerSeries:-Add, MultivariatePowerSeries
##AUTHOR Ali Asadi masadi4@uwo.ca, Alex Brandt abrandt5@uwo.ca, Marc Moreno Maza moreno@csd.uwo.ca
##
##CALLINGSEQUENCE
##- 'p1' + 'p2'
##- Add('P')
##- Add('P', 'coefopt')
##- 'u1' + 'u2'
##- Add('U')
##
##PARAMETERS
##- 'p1', 'p2' : power series generated by this package, polynomials, or complex constants
##- 'P' : sequence of power series generated by this package, polynomials, and complex constants
##- 'coefopt' : (optional) equation of the form ~coefficients = C~, where `coefficients` is a
##  keyword and 'C' is a list of polynomials and complex constants
##- 'u1', 'u2' : univariate polynomials over power series generated by this package which have the
##  same main variable, power series generated by this package, polynomials, or complex constants
##- 'U' : sequence of univariate polynomial over power series generated by this package which have
##  the same main variable, power series generated by this package, polynomials, or complex constants
##
##DESCRIPTION
##- The command ~p1 + p2~ returns the sum of the terms 'p1' and 'p2'. The result is a power series.
##- The command ~Add(P)~ returns the sum of the terms in 'P'.
##- The command ~Add(P, coefficients = C)~ returns the sum of the products ~C[i] * P[i]~. The
##  length of the list 'C' must be the same as the number of elements of 'P'.
##- The command ~u1 + u2~ returns the sum of the terms 'u1' and 'u2'. The result is a univariate
##  polynomial over power series.
##- The command ~Add(U)~ returns the sum of the entries of 'U'. They are converted to univariate
##  polynomials over power series in the same variable. If this is not possible, an error is
##  raised. This may happen if there are univariate polynomials over power series in different
##  variables. It can also happen if the univariate polynomials over power series all have the same
##  main variable, say 'x', but one of the other arguments is a power series that is not known to be
##  expressible as a polynomial in 'x'. The same restrictions apply to the calling sequence ~u1 +
##  u2~.
##
##INCLUDE assignment_warning.mi
##  
##EXAMPLES
##> with(MultivariatePowerSeries):
##- Create three power series.
##> a := GeometricSeries([x,y]):
##<(verification="type") object
##> b := PowerSeries(1+x+y+z):
##<(verification="type") object
##> c := PowerSeries(2*x*y + 3*z^3):
##<(verification="type") object
##- Create a power series representing the sum of _a_ and _b_.
##> a + b;
##<(verification="type") object
##- Add 1 to _a_.
##> a + 1;
##<(verification="type") object
##- Add _a_, _b_, _c_, and the polynomial _1 + x*y*z_.
##> Add(a,b,c,1+x*y*z);
##<(verification="type") object
##- Compute _a + 5*b + 10*c_.
##> Add(a,b,c,coefficients = [1, 5, 10]);
##<(verification="type") object
##- Create a univariate polynomial over power series, given by a polynomial.
##> f := UnivariatePolynomialOverPowerSeries(x*z + y*z^2 + x*y*z^3, z):
##<(verification="type") object
##- Add a polynomial to _f_. These two calling sequences are equivalent.
##> f + z + 3;
##<(verification="type") object
##> Add(f, z + 3);
##<(verification="type") object
##- Add a power series to 'f' that is independent of 'z' (and thus trivially polynomial in 'z').
##> f + GeometricSeries([x, y]);
##<(verification="type") object
##- Create a separate univariate polynomial over power series, and add it to 'f'.
##> g := UnivariatePolynomialOverPowerSeries([GeometricSeries([x,y]), PowerSeries(3)], z):
##<(verification="type") object
##> f + g;
##<(verification="type") object
##- This will raise an error, because we're trying to add univariate polynomials over power series
##  with different main variables.
##> h := UnivariatePolynomialOverPowerSeries([GeometricSeries([x,y]), PowerSeries(3)], w):
##<(verification="type") object
##> f + h;
##<(verification="testerror") "incompatible inputs: "
##- This also will not work, because Maple cannot determine that 'd' is polynomial in 'z' (though
##  actually it is).
##> d := PowerSeries(d -> ifelse(d = 0, 0, z * x^(d-1)/(d-1)!), variables = {x, z});
##<(verification="type") object
##> f + d;
##<(verification="testerror") "attempted to convert"
##- We define 'e' in the same way as 'd' but specify the analytic expression, and then we
##  can successfully add it to 'f'.
##> e := PowerSeries(d -> ifelse(d = 0, 0, z * x^(d-1)/(d-1)!), analytic = z * exp(x));
##<(verification="type") object
##> f + e;
##<(verification="type") object
##
##SEEALSO
##- "Subtract"
##- "PowerSeries"
##- "GeometricSeries"
##- "UnivariatePolynomialOverPowerSeries"
##- "MainVariable"
## 
##XREFMAP
##- Subtract : Help:MultivariatePowerSeries[Subtract]
##- PowerSeries : Help:MultivariatePowerSeries[PowerSeries]
##- GeometricSeries : Help:MultivariatePowerSeries[GeometricSeries]
##- UnivariatePolynomialOverPowerSeries : Help:MultivariatePowerSeries[UnivariatePolynomialOverPowerSeries]
##- MainVariable : Help:MultivariatePowerSeries[MainVariable]
