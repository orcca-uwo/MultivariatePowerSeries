##ENCODING ISO-8859-1
##PROCEDURE(help,nospec,label="MultivariatePowerSeries/ApproximatelyZero",versionnew="{XENOMORPH}",versionupdated="{YETI}") MultivariatePowerSeries[ApproximatelyZero]
##TITLE(halfline="determine equality with zero up to some precision")
##    MultivariatePowerSeries[ApproximatelyZero]
##ALIAS ApproximatelyZero, MultivariatePowerSeries:-ApproximatelyZero, MultivariatePowerSeries
##AUTHOR Ali Asadi masadi4@uwo.ca, Alex Brandt abrandt5@uwo.ca, Marc Moreno Maza moreno@csd.uwo.ca
##
##CALLINGSEQUENCE
##- ApproximatelyZero('p', 'deg', 'f')
##- ApproximatelyZero('u', 'deg', 'f')
##
##PARAMETERS
##- 'p' : power series generated by this package
##- 'u' : univariate polynomial over power series generated by this package
##- 'deg' : (optional) the precision up to which to compare
##- 'f': (optional) the keyword option 'force' or ~force = true~ or ~force = false~
##
##DESCRIPTION
##- Two power series 'p' and 'q' are said to be equal up to a degree 'deg', called the precision,
##  if for each degree 'd <= deg', the homogeneous components of 'p' and 'q' of degree 'd' are equal.
##- The command ~ApproximatelyZero(p)~ returns true if the power series is 
##  equal to zero up to its currently computed precision, otherwise false.
##- The command ~ApproximatelyZero(p,deg)~ returns true if the power series is 
##  equal to zero up to precision 'deg', otherwise false.
##- The command ~ApproximatelyZero(u)~ returns true if each coefficient of the univariate polynomial over
##  power series is equal to zero up to its currently
##  computed precision, otherwise false.
##- The command ~ApproximatelyZero(u,deg)~ returns true if each coefficient of the univariate polynomial over
##  power series is equal to zero up to precision 'deg', otherwise false.
##-(versionnew="{YETI}",name=force) By default, if 'p' knows its "analytic expression" and this analytic expression is zero, the
##  `ApproximatelyZero` command will immediately return true. If you pass the 'force' or ~force =
##  true~ option, then the `ApproximatelyZero` command will ignore the analytic expressions. The
##  default behavior can be explicitly selected by passing the option ~force = false~.
##- Any form of the 'force' option passed to the `ApproximatelyZero` command when comparing a
##  univariate polynomial over power series are used as described above when comparing the
##  power series coefficients.
##
##INCLUDE assignment_warning.mi
##
##EXAMPLES
##> with(MultivariatePowerSeries):
##- We create two power series and compute their difference.
##> a := Inverse(PowerSeries(1-x-y));
##<(verification="type") object
##> b := GeometricSeries([x,y]);
##<(verification="type") object
##> c := a - b;
##<(verification="type") object
##- The power series _a_ is known to have nonzero terms.
##> ApproximatelyZero(a);
##< false
##- In order to test whether _c_ has any nonzero terms of homogeneous degree 10 or less, we can
##  issue the following command. We see that _a_ and _b_ are the same up to homogeneous degree 10
##  (and indeed, from the analytic expression we can see that they are exactly equal).
##> ApproximatelyZero(c, 10);
##< true
##- Because _c_ is defined as a difference, we can test the same thing using the
##  "ApproximatelyEqual" command.
##> ApproximatelyEqual(a, b, 10);
##< true
##- The following two univariate polynomials over power series are exactly equal to 
##   _z / (1 + x + y)_. Hence, their difference is equal to 0 at any precision.
##> f := UnivariatePolynomialOverPowerSeries([PowerSeries(0), Inverse(GeometricSeries([x, y]))], z);
##<(verification="type") object
##> g := UnivariatePolynomialOverPowerSeries([PowerSeries(0), PowerSeries(1 - x - y)], z);
##<(verification="type") object
##> h := f - g;
##<(verification="type") object
##> ApproximatelyZero(h);
##< true
##> ApproximatelyZero(h, 10);
##< true
##- In this case, the analytic expressions for the coefficients are all zero. We can use the 'force'
##  option to make Maple do the actual computations.
##> ApproximatelyZero(h, 10, force);
##< true
##
##SEEALSO
##- "PowerSeries"
##- "GeometricSeries"
##- "Inverse"
##- "ApproximatelyEqual"
##- "IsUnit"
##- "Truncate"
##- "GetAnalyticExpression"
##
##XREFMAP
##- PowerSeries : Help:MultivariatePowerSeries[PowerSeries]
##- GeometricSeries : Help:MultivariatePowerSeries[GeometricSeries]
##- Inverse : Help:MultivariatePowerSeries[Inverse]
##- ApproximatelyEqual : Help:MultivariatePowerSeries[ApproximatelyEqual]
##- IsUnit : Help:MultivariatePowerSeries[IsUnit]
##- Truncate : Help:MultivariatePowerSeries[Truncate]
##- "analytic expression" : Help:MultivariatePowerSeries[GetAnalyticExpression]
