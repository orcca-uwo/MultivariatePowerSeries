##ENCODING ISO-8859-1
##PROCEDURE(help,nospec,label="MultivariatePowerSeries/GetAnalyticExpression",versionnew="{XENOMORPH}", versionupdated="{ZONKEY}") MultivariatePowerSeries[GetAnalyticExpression]
##TITLE(halfline="get an analytic expression for a power series or 
##	a univariate polynomial over power series or a Puiseux series") 
##    MultivariatePowerSeries[GetAnalyticExpression]
##ALIAS GetAnalyticExpression, MultivariatePowerSeries:-GetAnalyticExpression, MultivariatePowerSeries
##AUTHOR Ali Asadi masadi4@uwo.ca, Alex Brandt abrandt5@uwo.ca, Marc Moreno Maza moreno@csd.uwo.ca
##
##CALLINGSEQUENCE
##- GetAnalyticExpression('p')
##- GetAnalyticExpression('u')
##- GetAnalyticExpression('s')
##
##PARAMETERS
##- 'p' : power series generated by this package
##- 'u' : univariate polynomial over power series or over Puiseux series generated by this package
##-(versionnew="{ZONKEY}") 's' : Puiseux series generated by this package
##
##DESCRIPTION
##- The command ~GetAnalyticExpression(p)~ returns the analytic
##  expression of the power series 'p', if it is known, or 'undefined' if it is not known.
##- The power series that have a known analytic expression are the following:
##-- those created with the commands "GeometricSeries" and "SumOfAllMonomials";
##-- those created with the command "PowerSeries" from a polynomial, or from a
##   procedure while specifying the analytic expression explicitly;
##-- those obtained by applying
##  arithmetic operations (addition, multiplication, inversion, exponentiation)
##  to power series whose arithmetic expression is known.
##- The command ~GetAnalyticExpression(u)~ returns the analytic
##  expression of univariate polynomial 'u' over power series or over a Puiseux series, if it is known.
##  This is determined in the natural way from the analytic expressions of each coefficient
##  of 's'.
##- The command ~GetAnalyticExpression(s)~ returns the analytic
##  expression a Puiseux series 's', if it is known.
##  This is determined by taking the analytic expressions of its internal power
##	series and applying the change of variables to it.
##- If an analytic expression is known for a power series or a Puiseux series, it is part of the default display of that
##  power series. This is not the case for univariate polynomials over power series.
##
##INCLUDE assignment_warning.mi
##
##EXAMPLES
##> with(MultivariatePowerSeries):
##- Create a geometric power series in _x_ and _y_. Extract its analytic expression.
##> a := GeometricSeries([x,y]):
##<(verification="type") object
##> GetAnalyticExpression(a);
##< 1/(1-x-y)
##- Create a power series for a rational function. Extract its analytic expression.
##> b := Inverse(PowerSeries(3+2*x+y)):
##<(verification="type") object
##> GetAnalyticExpression(b);
##< 1/(3+2*x+y)
##- Define the product of _a_ and _b_. Its analytic expression is known because the analytic
##  expressions for both _a_ and _b_ are known.
##> c := a * b;
##<(verification="type") object
##> GetAnalyticExpression(c);
##< GetAnalyticExpression(a) * GetAnalyticExpression(b)
##- Below, _e_ and _f_ are defined as the same power series, but _f_ knows its analytic expression
##  and _e_ doesn't.
##> e := PowerSeries(d -> x^d/d!, variables={x});
##<(verification="type") object
##> GetAnalyticExpression(e);
##< undefined
##> f := PowerSeries(d -> x^d/d!, analytic=exp(x));
##<(verification="type") object
##> GetAnalyticExpression(f);
##< exp(x)
##- If we create power series from _e_ and _f_ by arithmetic operations, then those involving _e_ do
##  not know their analytic expressions, but those involving _f_ do (if the other power series
##  involved know their analytic expressions). Below, _g_ and _h_ represent the same power series,
##  but because _h_ used _f_ rather than _e_ in its definition, it knows its analytic expression.
##> g := a + b/e;
##<(verification="type") object
##> GetAnalyticExpression(g);
##< undefined
##> h := a + b/f;
##<(verification="type") object
##> GetAnalyticExpression(h);
##< GetAnalyticExpression(a) + GetAnalyticExpression(b) / exp(x)
##- If we create a univariate polynomial over power series, it will know its analytic expression if
##  each of the coefficients of the main variable knows its analytic expression. Below, _v_ knows
##  its analytic expression, but _u_ doesn't.
##> u := UnivariatePolynomialOverPowerSeries([a,b,e], z);
##<(verification="type") object
##> GetAnalyticExpression(u);
##< undefined
##> v := UnivariatePolynomialOverPowerSeries([a,b,f], z);
##<(verification="type") object
##> GetAnalyticExpression(v);
##< GetAnalyticExpression(a) + z*GetAnalyticExpression(b) + z^2 * exp(x)
##- Create a Puiseux series in _x_ and _y_. Extract its analytic expression.
##> s := PuiseuxSeries(PowerSeries(1/(1+U)), [U=x^(-1/3)*y^2], [x=3, y=-4]);
##<(verification="type") object
##> GetAnalyticExpression(s);
##< (x^3/y^4)*(1/(1+x^(-1/3)*y^2))
##- We can get the internal power series of _s_, get its analytic expression,
##	apply the change of variables given by _U=x^(-1/3)*y^2_ and multiply this 
##	by _x^3/y^4_.
##> p := GetPowerSeries(s);
##<(verification="type") object
##> (x^3/y^4)*eval(GetAnalyticExpression(p), U=x^(-1/3)*y^2);
##<	(x^3/y^4)*(1/(1+x^(-1/3)*y^2))
##- Finally, we create a univariate polynomial over power series from a list of Puiseux series.
##> h := UnivariatePolynomialOverPuiseuxSeries([PuiseuxSeries(1), PuiseuxSeries(0), PuiseuxSeries(x, [x=x^(1/3)]), PuiseuxSeries(y, [y=y^(1/2)]), PuiseuxSeries((x+y)/(1+x+y), [x=x*y^(1/2),y=x*y^(-1)])], z);
##<(verification="type") object
##- We get the analytic expression of _h_. 
##> GetAnalyticExpression(h);
##< 1+x^(1/3)*z^2+y^(1/2)*z^3+(x*y^(1/2)+x/y)/(1+x*y^(1/2)+x/y)*z^4
##
##SEEALSO
##- "PowerSeries"
##- "PuiseuxSeries"
##- "GeometricSeries"
##- "SumOfAllMonomials"
##- "Multiply"
##- "Inverse"
##- "Display"
##- "GetPowerSeries"
##
##XREFMAP
##- PowerSeries : Help:MultivariatePowerSeries[PowerSeries]
##- PuiseuxSeries : Help:MultivariatePowerSeries[PuiseuxSeries]
##- GeometricSeries : Help:MultivariatePowerSeries[GeometricSeries]
##- SumOfAllMonomials : Help:MultivariatePowerSeries[SumOfAllMonomials]
##- Multiply : Help:MultivariatePowerSeries[Multiply]
##- Inverse : Help:MultivariatePowerSeries[Inverse]
##- Display : Help:MultivariatePowerSeries[Display]
##-	GetPowerSeries : Help:MultivariatePowerSeries[GetPowerSeries]
